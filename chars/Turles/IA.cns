;===============================================================================
; INTELIGENCIA ARTIFICIAL:
;===============================================================================
[Statedef -2]

[State 0, Heal]
type = ChangeState
triggerall = life < 500
triggerall = var(3) = 0
Triggerall = power >= 2000
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x >= 100
triggerall = statetype != A
trigger1 = random < (ailevel * 100)
value = 1300

[State 0, Super God Shock Flash]
type = ChangeState
triggerall = power >= 3000
triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x < 60
trigger1 = random < (ailevel * 100)
value = 3000

[State 0, Run]
type = ChangeState
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x > 100
trigger1 = random < (ailevel * 100)/2
value = 60

[State 0, Run Atras]
type = ChangeState
triggerall = backedgebodydist > 50
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x < 100 && p2movetype = A
trigger1 = random < (ailevel * 100)/4
value = 70

[State 0, Guard]
type = ChangeState
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = random <=100
triggerall = (p2bodydist x < 50 && p2movetype = A) || (enemy,numproj > 0)
trigger1 = random < (ailevel * 100)/2
value = ifelse(statetype = A,132,130)

[State 0, Guard]
type = assertspecial
trigger1 = ailevel > 0 && roundstate = 2
flag = nostandguard
flag2 = nocrouchguard
flag3 = noairguard
ignorehitpause = 1

[State 0, Power Charge]
type = ChangeState
triggerall = power < 3000
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x > 100
trigger1 = random < (ailevel * 100)/4
value = 500

[State 0, Power Charge End]
type = ChangeState
triggerall = time >= 12
triggerall = stateno = 500
triggerall = ailevel > 0
trigger1 = (p2bodydist x < 50) || (enemy,numproj > 0)
trigger2 = power = 3000
value = 501

[State 0, Shock Wave]
type = ChangeState
triggerall = power >= 1000
triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x < 50
trigger1 = random < (ailevel * 100)
value = 1000

[State 0, Genkidama]
type = ChangeState
triggerall = power >= 1500
triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x >= 100
trigger1 = random < (ailevel * 100)
value = 1100

[State 0, Mad Combo]
type = ChangeState
triggerall = power >= 1500
triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl 
triggerall = p2bodydist x < 50
trigger1 = random < (ailevel * 100)
value = 1200

[State 0, Kamehameha]
type = ChangeState
triggerall = power >= 1000
triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x = [0,200]
trigger1 = random < (ailevel * 100)
value = 1400

[State 0, Upper Kamehameha]
type = ChangeState
triggerall = power >= 1000
triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x = [0,200]
trigger1 = random < (ailevel * 100)
value = 1500
;===============================================================================
;---------------------------Estados usados Siempre------------------------------
;===============================================================================
[Statedef -3]
[State 0, Helper de Conexiones]
type = Helper
Triggerall = numhelper(700) = 0
trigger1 = stateno = 200 && movecontact
trigger2 = stateno = 210 && movecontact
trigger3 = stateno = 310 && movecontact
trigger4 = stateno = 600 && movecontact
trigger5 = stateno = 610 && movecontact
trigger6 = stateno = 615 && movecontact
trigger7 = stateno = 1000 && time > 80
trigger8 = stateno = 1200 && time > 50
trigger9 = stateno = 3000 && time > 80
helpertype = normal
name = "Helper de Conexiones"
ID = 700
stateno = 700
pos = 999999,999999
postype = p1

[State 0, HELPER ESCALA]
type = Helper
trigger1 = NUMHELPER(999999) = 0
helpertype = normal
name = "HELPER ESCALA"
ID = 999999
stateno = 999999
pos = 9999,9999
postype = P1
facing = 1
keyctrl = 0
ownpal = 1
supermove = 999999
pausemove = 999999
supermovetime = 999999
pausemovetime = 999999

[State 0, Aura Permanente Kaioken]
type = Helper
triggerall = Alive
triggerall = var(3) = 1
trigger1 = numhelper(560) = 0
helpertype = normal
name = "Aura Permanente"
ID = 560
stateno = 561
pos = 0,0
postype = p1
facing = 1
keyctrl = 0
ownpal = 1
supermove = 999
supermovetime = 999
pausemove = 999
pausemovetime = 999
;===============================================================================
; Kaioken:
;-------------------------------------------------------------------------------
[State 0, EnvShake]
type = EnvShake
triggerall = var(3) = 1
trigger1 = movehit = 1
time = 5

[State 0, Pause]
type = Pause
triggerall = var(3) = 1
trigger1 = movehit = 1
time = 1

[State 0, AttackMulSet]
type = AttackMulSet
trigger1 = 1
value = 1+fvar(3)

[State 0, PowerAdd]
type = poweradd
Triggerall = stateno != 1860
triggerall = var(3) = 1
trigger1 = gametime%1 = 0
value = 2

[State 0, LifeAdd]
type = LifeAdd
Triggerall = stateno != 1860
triggerall = life > 0
triggerall = var(3) = 1 
trigger1 = gametime%10 = 0 
value = 3

[State 0, Kaioken Palfx]
type = PalFX
triggerall = Alive
triggerall = var(3) = 1
trigger1 = gametime%40 = 0
time = 100
add = 100,0,0
mul = 256,256,256
sinadd = 50,0,0,50
color = 256
ignorehitpause = 1
;-------------------------------------------------------------------------------
; COMUNES:
[State -3, VarSet,Apariencia]
type = VarSet
trigger1 = var(2) = 0
v = 11
value = 0

[State -3, VarSet,Golpeado]
type = VarSet
trigger1 = var(2) = 0
v = 10
value = 0

[State -3, VarSet, Specials]
type = VarSet
trigger1 = var(2) = 0
v = 14
value = 0
;-------------------------------------------------------------------------------
; FVARS:
;-------------------------------------------------------------------------------
[State 0, VarSet]
type = VarSet
trigger1 = var(3) = 0
fv = 3
value = 0

[State 0, VarSet]
type = VarSet
trigger1 = var(3) = 1
fv = 3
value = .2
;-------------------------------------------------------------------------------
; Sonidos Comunes:
;-------------------------------------------------------------------------------
[State 0, Saltando]
type = PlaySnd
triggerall = stateno = 40
trigger1 = Time = 1
value = S40,1
volume = 55
channel = -1

[State 0, Super Jump]
type = PlaySnd
triggerall = stateno = 80
trigger1 = Time = 1
value = S60,1
volume = 55
channel = -1

[State 0, Land]
type = PlaySnd
triggerall = stateno = 52
trigger1 = time = 1
value = S52,1
volume = 50

[State 0, Run]
type = PlaySnd
triggerall = stateno = 60||stateno = 70
trigger1 = time = 1
value = S40,2
volume = 50

[State 0, RemoveExplod]
type = RemoveExplod
TRIGGERALL = stateno != 500
trigger1 = time = 1
id = 550

[State 0, StopSnd]
type = StopSnd
TRIGGERALL = stateno != 500
trigger1 = time = 1
channel = 1

[State 0, StopSnd]
type = StopSnd
TRIGGERALL = stateno != 500
trigger1 = time = 1
channel = 2

[State 0, StopSnd]
type = StopSnd
TRIGGERALL = stateno != 500
trigger1 = time = 1
channel = 3

[State 0, StopSnd]
type = StopSnd
TRIGGERALL = stateno != 500
trigger1 = time = 1
channel = 4

[State 0, Golpeado]
type = PlaySnd
trigger1 = movetype=H && (stateno!=[0,155]) && !time && HitOver=0 && random%3=0
value = 5000,ifelse(random < 500,1,ifelse(random < 500,2,3))

[State 0, Solucion Stand Aire]
type = ChangeState
triggerall = stateno = 0||stateno = 20
trigger1 = pos y < 0
value = 50
ctrl = 1
